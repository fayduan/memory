import java.util.regex.Matcher
import java.util.regex.Pattern

apply plugin: 'com.android.application'
apply plugin: 'org.greenrobot.greendao'

android {
    compileSdkVersion 28
    buildToolsVersion "28.0.3"

    defaultConfig {
        applicationId "cn.duanyufei.memory"
        minSdkVersion 23
        targetSdkVersion 28
        versionCode 1
        versionName "v0.0.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        String version_tag = project.hasProperty("TRAVIS_TAG") ? TRAVIS_TAG : System.getenv("TRAVIS_TAG")
        if (version_tag != null) {
            Pattern pattern = Pattern.compile("v(\\d+)\\.(\\d+)\\.(\\d+)")
            Matcher matcher = pattern.matcher(version_tag)
            if (matcher.find()) {
                String vn = matcher.group(0)
                int v1 = Integer.parseInt(matcher.group(1))
                int v2 = Integer.parseInt(matcher.group(2))
                int v3 = Integer.parseInt(matcher.group(3))
                versionCode v1 * 1000000 + v2 * 1000 + v3 + 1
                versionName vn
            }
        }
    }
    signingConfigs {
        releaseConfig {
            storeFile file("../fayduan.jks")
            storePassword project.hasProperty("KEYSTORE_PWD") ? KEYSTORE_PWD : System.getenv("KEYSTORE_PWD")
            keyAlias project.hasProperty("ALIAS_NAME") ? ALIAS_NAME : System.getenv("ALIAS_NAME")
            keyPassword project.hasProperty("ALIAS_PWD") ? ALIAS_PWD : System.getenv("ALIAS_PWD")
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.releaseConfig
            applicationVariants.all { variant ->
                renameAPK(variant, defaultConfig)
            }
        }
        debug {
            applicationIdSuffix '.debug'
        }
    }
    buildTypes.each {
        it.buildConfigField 'String', 'FIR_TOKEN', project.hasProperty("FIR_TOKEN_STR") ? FIR_TOKEN_STR : System.getenv("FIR_TOKEN_STR")
    }
    greendao {
        schemaVersion 3
        targetGenDir 'src/main/java'
        daoPackage 'cn.duanyufei.greendao'
    }

}

def renameAPK(variant, defaultConfig) {
    variant.outputs.all { output ->
        outputFileName = "Memory_" + defaultConfig.versionName + ".apk"
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.squareup.okhttp:okhttp:2.7.5'
    implementation 'com.squareup.okio:okio:2.1.0'
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support:design:28.0.0'
    implementation 'com.github.castorflex.smoothprogressbar:library:1.1.0'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    implementation 'org.greenrobot:greendao:3.2.2'
//    compile 'com.idescout.sql:sqlscout-server:2.0'
    implementation 'com.ashokvarma.android:bottom-navigation-bar:2.1.0'
}
